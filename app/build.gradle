apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply from: 'variants.gradle'

def preDexEnabled = "true" == System.getProperty("pre-dex", "true")

android {
  compileSdkVersion Versions.compileSdk
  defaultConfig {
    applicationId "com.example.githubing"
    minSdkVersion Versions.minSdk
    targetSdkVersion Versions.targetSdk
    versionCode Versions.versionCode
    versionName Versions.versionName
    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
  }

  lintOptions {
    disable 'RestrictedApi'
  }

  dexOptions {
    preDexLibraries = preDexEnabled
  }

  kapt {
    correctErrorTypes = true
  }

  kotlinOptions {
    jvmTarget = "1.8"
  }

  dataBinding {
    setEnabled(true)
  }

  androidExtensions {
    experimental = true
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  sourceSets {
    androidTest.java.srcDirs += "src/test-common/java"
    test.java.srcDirs += "src/test-common/java"
  }
}

task getVersionName() {
  return "${Versions.versionMajor}.${Versions.versionMinor}.${Versions.versionPatch}"
}

dependencies {
  implementation Dependencies.support_appcompat
  implementation Dependencies.support_design
  implementation Dependencies.support_card_view
  implementation Dependencies.support_recycler_view
  implementation Dependencies.constraint_layout

  implementation Dependencies.kotlin_stdlib

  implementation Dependencies.arch_lifecycle
  implementation Dependencies.liveData_ktx
  implementation Dependencies.arch_room
  kapt Dependencies.arch_room_compiler
  testImplementation Dependencies.arch_core_testing
  testImplementation Dependencies.mock_web_server

  implementation Dependencies.dagger
  kapt Dependencies.dagger_compiler

  implementation Dependencies.dagger_android
  implementation Dependencies.dagger_android_support
  kapt Dependencies.dagger_android_compiler

  implementation Dependencies.rx_java
  implementation Dependencies.rx_android

  implementation Dependencies.preference_room
  kapt Dependencies.preference_room_compiler

  implementation Dependencies.gson

  implementation Dependencies.retrofit
  implementation Dependencies.retrofit_gson

  implementation Dependencies.timber
  debugImplementation Dependencies.memory_leak_debug

  implementation Dependencies.glide
  kapt Dependencies.glide_compiler

  testImplementation Dependencies.junit
  testImplementation Dependencies.mockito
  testImplementation Dependencies.mockito_kotlin
  androidTestImplementation Dependencies.mockito_android
  androidTestImplementation Dependencies.espresso

  // Add missing dependencies for JDK 9+
  if (JavaVersion.current().ordinal() >= JavaVersion.VERSION_1_9.ordinal()) {
    // Add both if you're using both Kotlin and Java

    // If you're using Kotlin
    kapt Dependencies.jaxb_core
    kapt Dependencies.jaxb_api
    kapt Dependencies.jaxb_impl

    // If you're using Java
    annotationProcessor Dependencies.jaxb_core
    annotationProcessor Dependencies.jaxb_api
  }
}